POLARIS_CLIENT_SECRET := "xxx"

generate-rsa-key-pair:
    openssl genrsa -out private.pem 4096
    openssl rsa -in private.pem -pubout -out public.pem

seal-secret:
    cat secrets/hm-polaris-secret.unsealed.yaml | \
    kubeseal \
        --controller-namespace=production-hm-sealed-secrets \
        --controller-name=sealed-secrets \
        --format=yaml \
        > kubernetes-manifests/hm-polaris-secret.yaml

bootstrap-polaris:
    kubectl run polaris-bootstrap \
        --namespace=production-hm-polaris \
        --image=apache/polaris-admin-tool:1.3.0-incubating \
        --restart=Never \
        --rm \
        --stdin \
        --tty \
        --env="quarkus.datasource.username=$(kubectl get secret hm-polaris-secret --namespace=production-hm-polaris --output=jsonpath='{.data.POSTGRES_USER_NAME}' | base64 --decode)" \
        --env="quarkus.datasource.password=$(kubectl get secret hm-polaris-secret --namespace=production-hm-polaris --output=jsonpath='{.data.POSTGRES_PASSWORD}' | base64 --decode)" \
        --env="quarkus.datasource.jdbc.url=$(kubectl get secret hm-polaris-secret --namespace=production-hm-polaris --output=jsonpath='{.data.POSTGRES_JDBC_URL}' | base64 --decode)" \
        -- \
        bootstrap \
            --realm=POLARIS \
            --credential=POLARIS,polaris_admin_oryx,{{ POLARIS_CLIENT_SECRET }}

list-principals:
    #!/usr/bin/env bash
    TOKEN=$(curl --silent --fail --show-error --location "https://polaris.hongbomiao.com/api/catalog/v1/oauth/tokens" \
        --data "grant_type=client_credentials&client_id=polaris_admin_oryx&client_secret={{ POLARIS_CLIENT_SECRET }}&scope=PRINCIPAL_ROLE:service_admin" \
        | jq --raw-output '.access_token')
    curl --silent --fail --show-error --location --header "Authorization: Bearer $TOKEN" \
        "https://polaris.hongbomiao.com/api/management/v1/principals" | jq .
